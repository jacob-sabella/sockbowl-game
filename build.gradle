import cz.habarta.typescript.generator.ClassMapping
import cz.habarta.typescript.generator.EnumMapping
import cz.habarta.typescript.generator.TypeScriptFileType
import org.springframework.boot.gradle.tasks.bundling.BootBuildImage

plugins {
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'cz.habarta.typescript-generator' version '3.2.1263'
}

group = 'com.soulsoftworks'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.3.0'
    implementation 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
    implementation 'io.github.openfeign:feign-okhttp:13.3'
    implementation 'io.github.openfeign:feign-httpclient:13.6'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'com.redis.om:redis-om-spring:0.8.9'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'org.projectlombok:lombok:1.18.38'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'com.github.victools:jsonschema-generator:4.38.0'
    implementation 'com.netflix.graphql.dgs.codegen:graphql-dgs-codegen-gradle:8.1.1'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'io.projectreactor:reactor-test'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:testcontainers:1.21.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.1'
    testImplementation 'com.redis.testcontainers:testcontainers-redis-junit-jupiter:1.4.6'
    testImplementation 'org.awaitility:awaitility:4.2.2'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

generateTypeScript {
    jsonLibrary = 'jackson2'
    classPatterns = ['com.soulsoftworks.sockbowlgame.model.**',]
    excludeClasses = [
            'com.soulsoftworks.sockbowlgame.model.state.GameSessionSanitizer',
            'com.soulsoftworks.sockbowlgame.model.socket.out.SockbowlMultiOutMessage'
    ]
    excludeClassPatterns = ['com.soulsoftworks.sockbowlgame.model.**Builder**']
    outputKind = 'module'
    outputFile = "build/typescript-generation/sockbowl-interfaces.ts"
    outputFileType = TypeScriptFileType.implementationFile
    mapEnum = EnumMapping.asEnum
    nonConstEnums = true
    mapClasses = ClassMapping.asClasses
    generateConstructors = true
}

test {
    useJUnitPlatform()
}

/* ----------------------------- Spring Boot image --------------------------- */
bootBuildImage {
    createdDate = "now"
    def repo = (System.getenv('GITHUB_REPOSITORY') ?: 'OWNER/REPO')
    imageName = "ghcr.io/${repo}:${project.version}"
}

/* ---------------------------- Dependency management ------------------------ */
dependencyManagement {
    imports { mavenBom "org.springframework.ai:spring-ai-bom:$springAiVersion" }
}

/* ------------------------------- Utilities -------------------------------- */
tasks.register('printVersion') {
    group = 'help'
    description = 'Prints the current project version'
    doLast { println project.version }
}

static String resolveVersion() {
    String gitRef = System.getenv('GITHUB_REF')
    if (gitRef?.startsWith('refs/tags/')) {
        String tag = gitRef.substring('refs/tags/'.length())
        return tag.startsWith('v') ? tag.substring(1) : tag
    }
    // Non-tag build: use latest tag as base -> X.Y.Z-SNAPSHOT; fallback to 0.0.0-SNAPSHOT
    String latestTag = execAndRead("git describe --tags --abbrev=0")
    if (latestTag) {
        String base = latestTag.startsWith('v') ? latestTag.substring(1) : latestTag
        return base + "-SNAPSHOT"
    }
    return "0.0.0-SNAPSHOT"
}

static String execAndRead(String cmd) {
    try {
        def p = cmd.execute()
        p.waitFor()
        return p.exitValue() == 0 ? p.text.trim() : null
    } catch (Exception ignored) {
        return null
    }
}

